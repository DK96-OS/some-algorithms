plugins {
    id 'java-library'
    id 'jacoco'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withJavadocJar()
    withSourcesJar()
}

tasks.sourcesJar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = "io.github.dk96-os.some-algorithms"
            artifactId = "maxdistance"
            version = "1.0"
            from components.java
        }
    }
}

dependencies {
    // JUnit 5
    testImplementation platform("org.junit:junit-bom:5.10.0")
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
}

sourceSets.main {
    java.srcDirs("src/main/java")
}

tasks.test {
    useJUnitPlatform() {
        excludeTags("slow")
    }
    maxParallelForks = 2
    testLogging {
        events("passed", "failed") // not including "skipped"
    }
    reports {
        junitXml {
            outputPerTestCase = true
            mergeReruns = true
        }
    }
}

tasks.jacocoTestReport {
    dependsOn tasks.test
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir("jacocoReport")
    }
}

tasks.jacocoTestCoverageVerification {
    dependsOn tasks.test
    violationRules {
        failOnViolation = true
        rule {
            limit {
                counter = "INSTRUCTION"
                minimum = 0.85
            }
            limit {
                counter = "BRANCH"
                minimum = 0.85
            }
        }
    }
}